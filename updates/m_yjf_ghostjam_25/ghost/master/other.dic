OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	//If you want to add anything to OnTranslate, like text replacement, I would put it after this point, generally. Just make sure it's before the TOSTR(_talk)
	
	TOSTR(_talk)
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	//elseif reference0 == "t"; OnAiTalk
	//elseif reference0 == "r"; OnLastTalk
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	/*
	if writemode == "on"	{
		case currentmode    {
    	    when 1; "\0\s[11]"
    	    when 2; "\0\s[21]"
    	    when 3; "\0\s[31]"
    	}
	}
	else	{
		case currentmode    {
    	    when 1; "\0\s[0]"
    	    when 2; "\0\s[1]"
    	    when 3; "\0\s[2]"
    	}
	}
	*/
}

//Happens when the ghost is unminimized. You can customize what surfaces are used here/if there's dialogue, but by default I've set it to just go to OnSurfaceRestore
OnWindowStateRestore
{
	//OnSurfaceRestore
}

OnMinuteChange
{
	_currenttime = GETSECCOUNT() - oldtime

	if writemode != "on"	{
		if _currenttime > 30	{
			_writechance = RAND(2) + 1

			if _writechance == 2;	OnWriteMode
		}
	}
	
	if _currenttime > 60	{
		_switchchance = RAND(5) + 1

		if _switchchance == 5;	OnSwitchMode
	}
}

OnSwitchMode
{
    _switchto = RAND(3) + 1
	
	while currentmode == _switchto; {_switchto = RAND(3) + 1}
    case _switchto    {
        when 1	{
			currentmode = 1
			
			if SHIORI3FW.LastSurface[0] == 21 || SHIORI3FW.LastSurface[0] == 31;	"\0\s[11]\i[4]"
			else;	"\0\s[0]\i[4]"
		}
        when 2	{
			currentmode = 2
			
			if SHIORI3FW.LastSurface[0] == 11 || SHIORI3FW.LastSurface[0] == 31;	"\0\s[21]\i[4]"
			else;	"\0\s[1]\i[4]"
		}
        when 3	{
			currentmode = 3
			
			if SHIORI3FW.LastSurface[0] == 11 || SHIORI3FW.LastSurface[0] == 21;	"\0\s[31]\i[4]"
			else;	"\0\s[2]\i[4]"
		}
    }
	
	oldtime = GETSECCOUNT()
}

OnWriteMode
{
	writemode = "on"

	LOGGING("+++++++++++++++")
	LOGGING("current mode: %(currentmode)")

	case currentmode    {
        when 1; "\0\s[11]"
        when 2; "\0\s[21]"
        when 3; "\0\s[31]"
    }
}

//Network update url. Make sure you have a / on the end of the url or it will give a warning every time users try to update! If you don't plan to use network updates you can remove this
//IF YOU'RE USING GITHUB FOR NETWORK UPDATES: DO NOT USE github.io URLS! It will cause you pain and suffering. USE raw.githubusercontent!!!
//To get the raw.githubusercontent link super easy:
//• Open your ghost's repo online
//• navigate to the ghost's folder (the top level one, with install.txt, readme.txt, etc)
//• Click on install.txt, or another text file on that level
//• Once on that page, click the "raw" button. You should see the url now starts with raw.githubusercontent
//• Copy that url exactly as is, but *leave off the "install.txt" or whatever file name at the end.*
//Tada! That's your url! Make sure you test it! The dev palette and script/network log can help with that
On_homeurl
{
	"https://raw.githubusercontent.com/singlefl4me/idk_ghosts/main/updates/m_yjf_ghostjam_25/"
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

On_sakura.recommendbuttoncaption
{
	"Main dev: idk"
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Github";	"https://github.com/singlefl4me"
	"Discord: @.idk_39";	"https://discord.com"
	"Email";	"mailto:idk.ghostdev@gmail.com"
}


//—————————————————————————————— User pronoun related ——————————————————————————————

they
{
	if SHIORI3FW.UserSex == "male"; "he"
	elseif SHIORI3FW.UserSex == "female"; "she"
	else; "they"
}

them
{
	if SHIORI3FW.UserSex == "male"; "him"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "them"
}

their
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "their"
}

theirs
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "hers"
	else; "theirs"
}

themself
{
	if SHIORI3FW.UserSex == "male"; "himself"
	elseif SHIORI3FW.UserSex == "female"; "herself"
	else; "themself"
}

//%(they) %(p('are','is'))
//You can change the name of this function if you want! I kept it short so it's fast to type
p
{
	if SHIORI3FW.UserSex == "undef"; _argv[0] //they are
	else; _argv[1] //he is/she is
}

/*

An example of the above setup:
"%(they) %(p('were','was')) looking for that earlier, I'll ask if %(they)'%(p('ve','s')) found it."

If using they, the %(p) functions will return the first word specified, resulting in:
"they were looking for that earlier, I'll ask if they've found it."

If not using they, for example if using she, it will return the second specified word instead, resulting in:
"she was looking for that earlier, I'll ask if she's found it."

With this function, you can quickly customize words that need to change based on whether it should be singular or plural. Just remember; the %(p) function is set up so that the first word you specify matches with the 'they' labels used on the pronoun functions. You can always switch that around if you like, too.

*/

//This just makes %(username) match with the username determined by the SSP prefs, rather than having to use the %username environment variable which is *not* the same and which you can't do things like TOUPPER to.
//If you want to have the user be able to set a nickname or something, you may want to change username to a variable instead and get rid of this!
username
{
	SHIORI3FW.UserName
}