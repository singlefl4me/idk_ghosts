//---------------------------AI Talk--------------------------


//--OnAiTalk

OnAiTalk
{
	if CHAIN.IDName == "" {
		if Flag("spookscene on") == 1 || Flag("scene in progress") == 1
			OnSpookTimeEx
		elseif spookmode == "on"
			OnSpookTime
		else
			lastTalk = RandomTalk
	}
	else
		lastTalk = ChainTalk

	lastTalk
}


//--RandomTalk 

RandomTalk: nonoverlap_pool
{	
	//add some dialogue here eventually
	"\0\s[0](@)You look wonderful today,\w5 Neighbor."
	"\0\s[0](@)I hope the day has treated you well."
	"\0\s[0](@)Do you have any special plans today?"
	"\0\s[8]"//possible chain?\![embed,OnChainEnding]
	"\0\s[5](@)\f[italic,1]ha\w4 ha,\w6 ha\w4 ha\w4 ha!\f[italic,0]\w8\n\s[0]\w8\nOh,\w5 sorry. \s[8]\w8Just thinking about something..."
	"\0\s[0](@)Are you doing alright?"
	"\0\s[8](@)Where are my friends?\w8 Where did they go.\w8.\w8.\w8"
	"\0\s[0](@)How was your day,\w6 Neighbor?"
	"\0\s[0](@)What do you like to do for fun?"
	//"\0\s[0](@)...?"
	//"\0\s[0](@)"
	//jokes/random obtuse pranks
	"\0\s[0](@)Did you know...\w6 you have something on your face.\n\w8\w8.\w8.\w8.\w8\f[italic,1]\s[5]ha\w4 ha\w4 ha\w4!\f[italic,0]\w8\s[0] I'm only kidding.\w8 Was that a good joke?"
	//"\0\s[0](@)..."
	//"\0\s[0](@)..."
	
	"\0\s[0](@)Are you having fun?\w8\n\.\w8.\w8.\w8\n\w8That's good,\w6 I think..."
	{
		"\0\s[0](@)Have you ever "
		--
		"read a book"
		"told jokes"
		"sang a song"
		"played a game"
		"spent time"
		--
		" with someone special?\n\w8I think thats the most!"
	}
	
	{//here too
		"\0\s[0](@)Can I tell you something?\n .\w8.\w8.\w8\n"
		--
		"oh,\w6 nevermind.\w8\s[8] Nothing to worry about..."
		"I'm glad you came to see me."
	}

	
	if dayslot == "10month 11day"	{
		"\0\s[0](@).\w8.\w8.\w8\w8\n\n[half]I don't know what it is...\n\n[half]\s[8]\w8Could it be...?"
		"\0\s[8](@)Do you know what today is, \w6Neighbor?\n\n[half]\w8Could you tell me?"
		"\0\s[8](@)Did I forget about something?"
		"\0\s[8](@)What could it be.\w8.\w8.\w8?"
		"\0\s[8](@)\f[italic,1]\s[5]ha\w4 ha\w4 ha\w4!\f[italic,0]\w8\s[0] What a funny feeling."
	}
}


//--spooktalk

spookTalk: nonoverlap_pool
{
	//here too
	"\0\s[0]\i[3000]\i[12]"
	"\0\s[3001]"
	--
	"(#)no one knows i'm here.\_w[800].\_w[800].\_w[800]\n.\_w[900]\n.\_w[900]\n.\_w[900]\n righ\w5t\w8?"
	"(#)i see you.\_w[4000]\_l[-65,110]hello"
	"(#)do\w8\w9 you\w8\w9\n\_l[,@20%] remember?\_l[@-275%,@20%] remember?\_l[@-275%,@20%] remember?\_l[@-275%,@20%] remember?\_l[@-275%,@20%] remember?\_l[@-275%,@20%] remember?\_l[@-275%,@20%] remember\n\n\n me\w9?"
	//"\![set,balloonwait,350%]\f[color,220,213,213]I WAS ALWAYS HERE"/edit l8r
	"(#)why won't you respond?"//edit l8r
	"(#)are you\w5 still\w8\w8 he\w2re\w8\w8?\![set,autoscroll,disable]\_w[5000]\n[1500]\![set,autoscroll,enable]\f[align,center]answer me\_l[@-5,@-2]answer me\_l[@4,@]answer me\_l[@-117,@3]answer me\_l[@,@5]answer me\_l[@-3,@-5]answer me\_l[@-134,@4]answer me\_l[@15,@-2]answer me\_l[@-75,@-2]answer me"
	"(#)do you feel it?"
	"(#)are\w8 you\w8 there\w8?"
	"(#)"

	{
		"\s[0](@)Can you do me a favor?\n .\w8.\w8.\w8\n"
		--
		".\w8.\w8.\w8\n.\w8.\w8.\w8\n.\w8.\w8.\w8\n.\w8.\w8.\w8\n.\w8.\w8.\w8\n.\w8.\w8.\w8\n\c\w[1000]"//edit l8r
		"let me in. please"//edit l8r
	}

	{
		"\0\s[0](@)Can I tell you something?\n .\w8.\w8.\w8\n"
		--
		"please help me"
		"are you still there?"
		//"what is it like? to feel... hurt"
	}
	
	/*
	//for inspo. may add in l8r
	"don't move"
	"where are you""i cant"
	"i know who you are"
	"are you out there? can you see me?"

	
	*/
}


//--extras

spook_scenario: nonoverlap_pool
{
	if Flag("scene in progress") == 1	{
		if Flag("scene_a") == 1	{
			//base : "\0\s[0](@).\w8.\w8.\w8\w8\n\n[half]"
			"\0\s[0](@).\w8\w8.\w8\w8.\w8\w8\n\n[half]\w8Well,\w6 I don't know about \w3\f[italic,1]that\f[italic,0]\w3..."
			"\0\s[0](@).\w8\w8.\w8\w8.\w8\w8\n\n[half]\w8Oh,\w6 really?\w8\n\n[half].\w8\w8.\w8\w8.\w8\w8"
			"\0\s[0](@).\w8\w8.\w8\w8.\w8\w8\n\n[half]\w8Well, \w6what do you think?\w8\n\n[half].\w8\w8.\w8\w8.\w8\w8"
			"\0\s[0](@).\w8\w8.\w8\w8.\w8\w8\n\n[half]\w8Oh,\w5 of course!\w8\n\n[half].\w8\w8.\w8\w8.\w8\w8"
			"\0\s[0](@).\w8\w8.\w8\w8.\w8\w8\n\n[half]\w8Do you think so?\w8\n\n[half].\w8\w8.\w8\w8.\w8\w8\n\n[half]\w8Alright,\w5 then."
			"\0\s[0](@).\w8\w8.\w8\w8.\w8\w8\n\n[half]\w8That does sound nice..."
			"\0\s[0](@).\w8\w8.\w8\w8.\w8\w8\n\n[half]\w8"
			//".\w8.\w8.\w8\e:chain=scenario_a"
		}

	}
}


//--Chains

scenario_a
{{CHAIN
	"%(homesounds)"
	"%(homesounds)"
	"%(homesounds)"
	"%(homesounds)"
	"%(homesounds)\![raise,OnChainInt,30]"
	"%(homeappear)"
	"%(homeappear)"
	"%(homeappear)"
	"%(homeappear)"
	"%(homeappear)"
	"%(homeappear)\_w[2500]%(eye_l)\_w[2500]%(eye_r)\_w[2500]%(eye_l)\_w[2500]%(eye_c)\_w[1000]\0\s[0]Oh,\w5 hello Home. \w8It's nice of you to join us.\xNeighbor,\w6 this is Home.\x[noclear]\p[2]%(eye_d.r)\0\w8 Home is my friend!\xHome seemed \w3\f[italic,1]very\f[italic,0]\w3 excited to meet you. \w8It's always nice to meet a new friend.\x\p[2]%(eye_c)\_w[1500]\0I hope you two can be \w3\f[italic,1]great\f[italic,0]\w3 friends someday...\w8\n\w8\n[half]Wouldn't that be nice,\w6 Neighbor?\![raise,OnChainReset]\e:chain=end"
}}CHAIN


//--Chain Functions

OnChainInt
{
	if !ISVAR("temptalkinterval")
		StoreInterval

	aitalkinterval = reference0
}

OnChainReset
{
	StoreInterval
	RemoveFlag("scene in progress")
	RemoveFlag("scene_a")
	AddFlag("scene_a complete")
	--
	"\w8\w8\-"
}


//--OnSpookTime

OnSpookTime
{
	_spooktime

	if Flag("scene in progress") == 1	{
		lastTalk = spook_scenario
	}

	if ghostupmin >= 60 && ghostupmin < 120	{
		_spooktime = RAND(6)
		if _spooktime == 0	
			lastTalk = spookTalk
		else
			lastTalk = RandomTalk
	}
	elseif ghostupmin >= 120 && ghostupmin < 180	{
		if spookchance == 1	{
			_spooktime = RAND(4)
			if _spooktime == 0	
				lastTalk = spookTalk
			else
				lastTalk = RandomTalk
		}
		elseif spookchance != 1
			lastTalk = RandomTalk
	}
	elseif ghostupmin >= 180	{
		if spookchance == 1	{
			_spooktime = RAND(3)
			if _spooktime == 0	
				lastTalk = spookTalk
			else
				lastTalk = RandomTalk
		}
		elseif spookchance != 1
			lastTalk = RandomTalk
	}
	else
		lastTalk = RandomTalk
}

OnSpookTimeEx
{
	_spooktime


	if Flag("scene in progress") == 1	{
		spookcount++
		
		if spookcount >= 4
			lastTalk = ".\w8.\w8.\w8\![raise,OnChainInt,180]\e:chain=scenario_a"
		
		else
			lastTalk = spook_scenario
	}

	elseif ghostupmin > 10 && Flag("scene in progress") == 0	{
		if ghostupmin < 60 	{
			_spooktime = RAND(6)

			if _spooktime == 0	{
				RemoveFlag("spookscene on")
				AddFlag("scene in progress")
				lastTalk = spook_scenario
			}
			else
				lastTalk = RandomTalk
		}
		elseif ghostupmin >= 60	{
			RemoveFlag("spookscene on")
			AddFlag("scene in progress")
			lastTalk = spook_scenario
		}
	}
	else
		lastTalk = RandomTalk
}


//-OnMinuteChange

HourCheck
{
if passmin >= 59
	{
		passmin = 0
		passhour = passhour + 1
		
		spookchance = RAND(2)
	}
}


OnMinuteChange
{
	HourCheck //checks to see if an hour has passed, ignore it
}


//--OnSecondChange

OnSecondChange
{
	if passsec == 59
 		{
	 	passsec = 0
 		passmin = passmin + 1
		}
 	else
 		{
	 	passsec = passsec + 1
		}
}

//**** OnSurfaceRestore *****************************************************************

OnSurfaceRestore
{
 	//if lastTalk == "\0\s[8]"
 		//OnFakeChain
 	//else	
	"\0\s[0]\1\s[-1]\e"
	//
}

 
//--OnKeyPress
 
OnKeyPress
{
	if reference0 == "t"
		OnAiTalk
	elseif reference0 == "r"
		OnLastTalk
	elseif reference0 == "f1"
		"\![open,readme]"
}