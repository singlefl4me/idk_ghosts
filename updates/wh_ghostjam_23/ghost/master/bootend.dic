//---------------------------Booting and Ending--------------------------


//--OnFirstBoot--

OnFirstBoot
{
 	lastTalk = ""	//This is part of how chained conversations work (see aitalk.dic).

	passhour = 0
	passmin = 0 
	passsec = 0		//These keep track of the time.
	
	stroke           = 0	// Sets their being-pet state to zero.
	mikireflag       = 0	// Determines behavior at certain points.
 	aitalkinterval = 600	//Their default talk rate.
	talktime = "every 10 minutes" //How the value aitalkinterval will display in the config menu.
	
	spookmode = "off"
	spookchance = 0
	spookcount = 0
	MiscFlags = IARRAY
	//visitcount = 0	//count how many times the ghost has been opened. using for special (not spooky) onboot dialogue

	if reference0 == 0 
 	{
		//might replace later, might not
		"\1\s[-1]\p[2]\s[-1]"
		--
		"\![enter,nouserbreakmode]\0\s[0](@)Oh.\w8 Hello.\w8 Didn't see you there.\n\w8\n[half]\w8I don't believe we met.\w8 I'm Wally.\w8\w8 It's always nice to meet a new neighbor,\w6 \s[5]\f[italic,1]ha\w5 ha\w5 ha...\f[italic,0]\w8\s[0]\n\n[half]I hope we can be good friends,\w6 Neighbor.\![leave,nouserbreakmode]"
 	}
	else
 	{
 		""
 	}
}

//--timeslot

timeslot
{
	if hour >= 5 && hour <= 8
		"earlymorning"
	elseif hour >= 9 && hour <= 11
		"morning"
	elseif hour >= 12 && hour <= 14
		"lunch"
	elseif hour >= 15 && hour <= 17
		"afternoon"
	elseif hour >= 18 && hour <= 20
		"evening"
	elseif hour >= 21 && hour <= 24
		"latenight"
	else
		"midnight"
}

//--dayslot

dayslot
{
	"%(month)month %(day)day"
}


//-----------------------Normal Booting----------------------------
/*
OnInitialize
{
	if reference0 != "reload" //For things you want to reset EXCEPT when just reloading; a convenience for you the dev, basically
	{	
		passhour = 0
		passmin = 0
		passsec = 0

		visitcount++
		
		LastTalk = ""
	}
}
*/
//--OnBoot

OnBoot
{
	lastTalk = "" 
	passhour = 0
	passmin = 0
	passsec = 0

	spookcount = 0
	--
	OnSpecialBoot
	--
	"\1\s[-1]\p[2]\s[-1]"
	--
	"\0\s[0](@)Hello,\w5 neighbor...!"
	"\0\s[0](@)Did you miss me,\w5 Neighbor?"
	"\0\s[0](@)It's good to see you again."
	"\0\s[0](@)What brings you here today?"
	--
	if dayslot == "10month 11day"	{
		"\_w[3000]\n\n[half].\w8.\w8.\w8\w8\n\n[half]\s[8]Something feels... \w6\f[italic,1]\w2different\w2\f[italic,0] about today. \w8It almost feels like... \w6something \f[italic,1]\w2special\w2\f[italic,0] happened today. \_w[1500]\n\n[half]\f[italic,1]\s[5]ha\w4 ha\w4 ha\w4!\f[italic,0], \w6\s[0]oh well."
	}
}

//--OnClose

OnClose
{
	if dayslot == "10month 11day"	{
		"\0\s[0](@)Oh well, \w5I'll leave it alone. \w8Whatever it was, \w6today must have been a \f[italic,1]\w2very\w2\f[italic,0] special day. \w8\w8Until next time, \w6Neighbor..."
	}
	else	{
		"\0\s[0](@)See you soon..."
		"\0\s[0](@)Until we meet again..."
		"\0\s[0](@)Goodbye..."
		"\0\s[0](@)I'll be seeing you..."
		"\0\s[0](@)I'm sure we will meet again soon..."
		"\0\s[0](@)That was fun. I hope to see you again."
	}
	
 	--
	"\w8\w8\-" //this bit of code actually closes and ends the ghost. Because \e is here, you don't have to end each line above with it.
}

OnSpecialBoot
{
	if spookmode == "on" && Flag("spookscene on") == 0	{
		_spooktime

		if Flag("scene in progress") == 0	{
			if Flag("scene_a complete") == 0	{//replace with case/when
				_spooktime = RAND(4)
				if _spooktime == 0	{
					AddFlag("spookscene on")
					AddFlag("scene_a")
				}
			}
		
		}
		//when adding more scenes, use case/when to decide between them
	}
}