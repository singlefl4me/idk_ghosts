OnFirstBoot
{
	//If you add variables, make sure to initialize them here!
	//Note - I haven't added anything about username or pronouns here because it's all handled by the SSP prefs
	stroke = 0
	LastTalk = ""
	if !ISVAR(firstboot); firstboot = 1
	
	"\1\s[10]\0\s[0]\![bind,lighting,off,1]\_w[10000]\i[103]"
}

OnInitialize
{
	//If you want some variables to reset on boot, put them here. This event will happen even if you add other events like OnGhostChanged
	//OnInitialize is a NOTIFY event! That means it cannot return scripts. This event is only meant for things like variable changes. See this page on Ukadoc for more info: https://ukagakadreamteam.github.io/ukadoc/manual/spec_shiori3.html
	
	//If you add new variables in updates, I recommend initializing them like so:
	//if !ISVAR("NewVariable"); NewVariable = 0
	//This means that it will only set the new variable if the user doesn't already have it, so that their variables don't get overwritten
	
	if reference0 != "reload"	{
		LastTalk = ""
	}
}

OnBoot
{
	if firstboot == 0	{
		"\0\![bind,lighting,on,1]"
		--
		"\s[5]Oh, thank goodness! You're here."
		"\s[5]You're back!"
		"\s[0]What happened? \w8\s[2]Did you need to step out or something...?"
	}
	else	{
		"\![raise,OnFirstBoot]"
	}
	
}

OnClose
{
	if firstboot == 0	{
		"\0\![bind,lighting,on,0]\_w[3000]"
		--
		"\s[2]Huh???"
		"\s[4]Please don't leave..."
		"\s[2]Wait a minute!"
		"\s[4]Oh..."
		"\s[3]Where did you go...?"
	}
	--
	"\w8\w8\-" //This is what makes the ghost close (after a short pause), do not remove the \- , and MAKE SURE you don't use a \e before this point or it won't work
}