//---------------------------Menus--------------------------


//--OpenSakuraMenu

OpenSakuraMenu: all
{
	wd = GetWeekDay
 	ap = GetAP
	//These four statements basically get the day of the week, whether it's AM or PM, the hour, and the time. It's putting together the information to display the time at the bottom of the balloon.
	"\n\n\_q\b2" 
	if lastTalk != ""
	  "\![*]\q[Repeat Last Dialogue,OnLastTalk]\n\n[half]"

 	"\![*]\q[Say something,OnAiTalk]\n"

	"\f[bold,1]Talk Rate:\f[bold,0] - %(talktime) \f[sup,1](\q[Change,talkinter])\f[sup,0]\n\n[half]"
	if spookmode == "on"	
		"\![*]\q[Disable Horror Content,spookoff]\n\n[half]\n"
	elseif spookmode == "off"
		"\![*]\q[Enable Horror Content,spookon]\n\n[half]\n"

	//"\![*]\q[Test Variable,testvariable]\n"

 	"\![*]\q[Close,OnMenuClose]\n\n\n[half]%(wd) %(nowhour):%(nowmin) %(ap), %(month)/%(day)/%(year)"
}

//Leave this alone
OnLastTalk 
{
"%(lastTalk)"
}

//This is figuring out what day of the week it is. Leave it alone.
GetWeekDay
{
	if weekday == 0;	"Sun"
	elseif weekday == 1;	"Mon"
	elseif weekday == 2;	"Tue"
	elseif weekday == 3;	"Wed"
	elseif weekday == 4;	"Thu"
	elseif weekday == 5;	"Fri"
	else;	"Sat"
}

//This is finding out whether it's AM or PM. Leave it alone.
GetAP
{
 	if hour >= 0 && hour <= 11;	"AM"
 	else;	"PM"
}

nowhour
{
	if hour12 == 0;	"1"
	else;	"%(hour12)"
}

nowmin
{
	if minute >= 0 && minute <=9; "0%(minute)"
	else; "%(minute)"
}


//If someone selects an option linked to BACKTOMENU, it will go back to OpenSakuraMenu.
Select.BACKTOMENU
{
	OpenSakuraMenu
}


//--Talk Rate
//This is when the user chooses to adjust the talk rate in the config menu. The talk rate is how often the ghost will read some dialogue from aitalk.dic.

Select.talkinter
{
	"\0\s[0]The interval is currently set to %(talktime)./
	\0\n\n[half]/
	\![*]\q[Every 30 seconds,TALKMAX]\n/
	\![*]\q[Every minute,TALKHIGH]\n/
	\![*]\q[Every 3 minutes,TALKNORMAL]\n/
	\![*]\q[Every 5 minutes,TALKPOOR]\n/
	\![*]\q[Don't speak,TALKNONE]\n\n[half]/
	\![*]\q[Don't change,TalksTill]\e"
}

//If the user doesn't want to change the rate.
Select.TalksTill
{
	"(The interval is now %(talktime).)"
}

//If the user chooses every 30 seconds.
Select.TALKMAX
{
	talktime = "every 30 seconds" //this sets up the envelope properly
	aitalkinterval = 30 //this changes the actual rate

	"(The interval is now %(talktime).)"
}

//If the user chooses every minute.
Select.TALKHIGH
{
	talktime = "every minute"
	aitalkinterval = 60

	"(The interval is now %(talktime).)"
}

//If the user chooses every three minutes.
Select.TALKNORMAL
{
	talktime = "every 3 minutes"
	aitalkinterval = 180

	"(The interval is now %(talktime).)"
}

//If the user chooses every five minutes. This is the default rate the ghost will start up in, as seen in bootend.dic.
Select.TALKPOOR
{
	talktime = "every 5 minutes"
	aitalkinterval = 300

	"(The interval is now %(talktime).)"
}

//If the user chooses to not have them speak.
Select.TALKNONE
{
	talktime = "disabled"
	aitalkinterval = 0

	"(The interval is now %(talktime).)"
}

//If the user selects cancel in a menu.
Select.CANCEL
{
	"OpenSakuraMenu"
}


OnMenuClose
{
	""
}

Select.spookon
{
	spookmode = "on"
	OpenSakuraMenu
}

Select.spookoff
{
	spookmode = "off"
	OpenSakuraMenu
}

Select.testvariable
{
	spookmode = "off"
}
